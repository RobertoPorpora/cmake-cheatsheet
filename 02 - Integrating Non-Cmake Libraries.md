# Integrating Non-CMake Libraries

> Instructions on how to integrate the inih library into a hypothetical project: https://github.com/benhoyt/inih/

To better explain the basic concepts and demonstrate a minimal application, I downloaded only two files from GitHub:
- ini.c
- ini.h

I will integrate them into a project called *pippo*, which is an executable that will use *inih* and other generically named libraries, *Lib001* and *Lib002*.  
Regarding these libraries, we just need to know that:
- *Lib001* exports a `library STATIC` target called `lib1`
- *Lib002* exports a `library STATIC` target called `lib2`

## Objective
This example is independent of the content of src.  
We only need to know that by setting up the project this way, it will be possible to write `#include "ini.h"` and use the functions contained in ini.h within the source files.  
The goal is precisely this: to be able to call the functions of *ini.h* within *file1.c* (or other files in *pippo/src*).

## Folder Structure

```
pippo
	CMakeLists.txt
	src
		main.c
		file1.h
		file1.c
		file2.h
		file2.c
	vendor
		Lib001
            CMakeLists.txt
		Lib002
            CMakeLists.txt
		inih
			CMakeLists.txt
			src
                ini.c
            include
                inih
                    ini.h
	build
	bin
```

Clarifications:
- The *pippo/build* and *pippo/bin* folders are generated by CMake.
- The *pippo/vendor/Lib001/CMakeLists.txt* file was already present in *Lib001*, we do not need to modify it.
- The same goes for the *pippo/vendor/Lib002/CMakeLists.txt* file.
- We prepare the *pippo/vendor/inih* folder and its content from scratch.
    - The *ini.c* and *ini.h* files are copied directly from GitHub.
    - We write the *pippo/vendor/inih/CMakeLists.txt* file ourselves, see below.
- Naturally, we also write the *pippo/CMakeLists.txt* file, see below.

## *pippo/vendor/inih/CMakeLists.txt*
```CMake
cmake_minimum_required(VERSION 3.26)
project(inih)

# create the 'inih' target as a static library
add_library(inih STATIC)

# add the source files for the 'inih' target
target_sources(inih
PRIVATE
    src/ini.c
    include/inih/ini.h
)

# declare that
# in the path 'include/inih', there are header files (.h)
# to be used for the 'inih' target, hence it cannot be INTERFACE.
# we need them to be usable by the 'pippo' target as well, therefore PUBLIC.
target_include_directories(inih
    PUBLIC include/inih
)
```

## *pippo/CMakeLists.txt*
```CMake
cmake_minimum_required(VERSION 3.26)
project(pippo)

# create the 'pippo' executable target
add_executable(pippo)

# declare the sources of 'pippo'
target_sources(pippo
PRIVATE
    src/main.c
    src/file1.h
    src/file1.c
    src/file2.h
    src/file2.c
)

# execute the CMakeLists.txt files contained in 'pippo's dependencies
# the folders will be compiled and a target will be obtained for each folder.
add_subdirectory(vendor/Lib001)
add_subdirectory(vendor/Lib002)
add_subdirectory(vendor/inih)

# create the links between 'pippo' and its dependencies (now available as targets)
target_link_libraries(
pippo
    PRIVATE lib1
    PRIVATE lib2
    PRIVATE inih
)

# request to use the 'bin' folder
# as the output for the compilation of the 'pippo' target

set_target_properties(
pippo
    PROPERTIES
    RUNTIME_OUTPUT_DIRECTORY "${CMAKE_SOURCE_DIR}/bin"
)
```